buildscript {
    ext {
        // external dependency versions
        dokkaPluginVersion = '0.9.8'
        groovyVersion = '2.4.5'
        gradleVersion = '2.9'
        kotlinVersion = '1.0.2'
        licensePluginVersion = '0.13.1'
        spockVersion = '1.0-groovy-2.4'

        // external dependencies
        groovy = "org.codehaus.groovy:groovy-all:${groovyVersion}"
        kotlinStdLib = "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
        spockCore = "org.spockframework:spock-core:${spockVersion}"
    }

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokkaPluginVersion}"
        classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:${licensePluginVersion}"
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = gradleVersion
}

group = 'com.github.jonpeterson'
version = '1.0.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'groovy'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.github.hierynomus.license'

sourceCompatibility = 1.6

repositories {
    mavenCentral()
}

dependencies {
    compile kotlinStdLib

    testCompile groovy
    testCompile spockCore
}

license {
    include '**/*.kt'
    include '**/*.groovy'
    mapping {
        kt = 'SLASHSTAR_STYLE'
        groovy = 'SLASHSTAR_STYLE'
    }
}

dokka {
    outputDirectory = 'docs'
    linkMapping {
        dir = 'src/main/kotlin'
        url = 'https://github.com/jonpeterson/kotlin-range-sets/blob/master/src/main/kotlin'
        suffix = '#L'
    }
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task sourcesJar(type: Jar) {
    dependsOn dokkaJavadoc
    classifier = 'sources'
    from "$buildDir/javadoc"
}

build {
    dependsOn dokka
    dependsOn sourcesJar
}